import org.joda.time.*
import java.util.Date

var DateTime	lastMotion

rule "captureLastMotion"
when
	Item CMotion changed from OFF to ON
then
	lastMotion = now
	sendCommand(CMotionLastMinutes, 0);
	logInfo("Sufit", "Wykryto ruch o " + lastMotion)
end

rule "calculateLastMotionMinutes"
when
	Time cron "0 */1 * ? * * *"
then
	var lastMotionMinutes = 0
	if (lastMotion != null) lastMotionMinutes = Minutes.minutesBetween(lastMotion, now).getMinutes()
	sendCommand(CMotionLastMinutes, lastMotionMinutes);
	logInfo("Sufit", "Od ostatniego ruchu upłyęło minut: " + lastMotionMinutes)
end


rule "whenWorkHours_noMotion_turnLightsOff"
when
	Time cron "0 */5 9-17 ? * Mon-Fri *"
then
	if (CMotionLastMinutes.state >= 30 && CLights.state == ON) {
		logInfo("Sufit", "Wyłącz światło - sufit")
		sendCommand(CLights, OFF)
	}	
end
