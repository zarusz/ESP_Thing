rule "Tryb - start"
when 
	System started
then
	logInfo("Mode", "Tryb: normalny")
	sendCommand(Mode, "Normal")
end

rule "Tryb - zmiana"
when
	Item Mode received update
then
	logInfo("Ceiling", "Turning all lights off")
	if (Mode.state == "Normal") {
		sendCommand(SleepLevel, 0)
	}

	if (Mode.state == "Sleeping" || Mode.state == "Away") {
		sendCommand(SleepLevel, 4)
		if (Lights.state == ON) sendCommand(Lights, OFF)
		if (Yamaha_Power.state == ON) sendCommand(Yamaha_Power, OFF)
	}
end

rule "Tryb - ustaw spanie, jeżeli 1 rano i brak ruchu od 20 minut"
when
	Time cron "00 */5 00-06 ? * *"
then
	// see http://www.quartz-scheduler.org/documentation/quartz-2.x/tutorials/tutorial-lesson-06
	if (CMotionLastMinutes.state >= 20 && Mode.state == "Normal") {
		logInfo("Mode", "Tryb: spanie")
		sendCommand(Mode, "Sleeping")
	}	
end

rule "Tryb - ustaw normalny, jeżeli 7 rano i spanie"
when
	Time cron "00 00 07 ? * *"
then
	if (Mode.state == "Sleeping") {
		logInfo("Mode", "Tryb: normalny")
		sendCommand(Mode, "Normal")
	}
end

rule "Tryb - ustaw poza, jeżeli 9 rano i brak ruchu"
when
	Time cron "00 */5 09 ? * MON-FRI"
then
	if (CMotionLastMinutes.state >= 30 && Mode.state == "Normal") {
		logInfo("Mode", "Tryb: poza domem")
		sendCommand(Mode, "Away")
	}	
end

rule "Tryb - ustaw normalny, jeżeli 17 rano i było poza"
when
	Time cron "00 00 17 ? * *"
then
	if (Mode.state == "Away") {
		logInfo("Mode", "Tryb: normalny")
		sendCommand(Mode, "Normal")
	}	
end

